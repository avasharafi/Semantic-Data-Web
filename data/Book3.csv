Members of the French National Assembly born out of France,"SELECT DISTINCT ?item ?itemLabel ?placeLabel ?countryLabel
WHERE
{
  ?item wdt:P39 wd:Q3044918 .
  ?item wdt:P19 ?place .
  ?place wdt:P17 ?country .
  FILTER NOT EXISTS { ?place wdt:P17 wd:Q142 . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language ""fr"" . }
}
ORDER BY ?countryLabel ?itemLabel",https://query.wikidata.org/sparql
Number of jurisdictions by driving side,"SELECT ?sideLabel (COUNT(?jurisdiction) AS ?count)
WHERE
{
  ?jurisdiction wdt:P1622 ?side.
  SERVICE wikibase:label { bd:serviceParam wikibase:language ""en"". }
}
GROUP BY ?sideLabel
ORDER BY ?sideLabel",https://query.wikidata.org/sparql
Number of dead people by month since 2000,"SELECT  ?yearmonth (COUNT(?person) as ?count)
WHERE
{
  ?person wdt:P31 wd:Q5;
          p:P570/psv:P570 [
                wikibase:timePrecision ?precision ;
                wikibase:timeValue ?date ;
              ] .
  BIND(CONCAT(STR(YEAR(?date)),""-"",STR(MONTH(?date))) as ?yearmonth).
  FILTER( ?date >= ""2000-01-01T00:00:00""^^xsd:dateTime )
  FILTER( ?precision >= ""10""^^xsd:integer ) # precision of at least month
}
GROUP BY ?yearmonth",https://query.wikidata.org/sparql
Number of films starring leonardo Di caprio.,"PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT COUNT(DISTINCT ?uri) 
WHERE {
 ?uri rdf:type dbo:Film .
        ?uri dbo:starring res:Leonardo_DiCaprio . 
}",http://dbpedia.org/sparql
20th-century earthquakes,"SELECT
?earthquakeItem ?earthquake ?coord ?year
(xsd:integer(?mercalliScaleString) AS ?mercalliScale) 
?numOfDeath
WHERE {
  ?earthquakeItem wdt:P31 wd:Q7944 .
  OPTIONAL { ?earthquakeItem wdt:P625 ?coord . }
  OPTIONAL { ?earthquakeItem wdt:P585 ?time . }
  OPTIONAL { ?earthquakeItem wdt:P1120 ?numOfDeath . }
  OPTIONAL { ?earthquakeItem wdt:P2784 [ p:P361 [ps:P361 wd:Q170350 ; # Mercalli intensity scale
                                                 pq:P1545 ?mercalliScaleString ] ] . }
  BIND (YEAR(?time) AS ?year) .
  FILTER (?year >= 1900 && ?year < 2000) .
  SERVICE wikibase:label { bd:serviceParam wikibase:language ""en"".
                           ?earthquakeItem rdfs:label ?earthquake . }
}
ORDER BY DESC(?mercalliScale)",https://query.wikidata.org/sparql
All museums (including subclass of museum) in Barcelona with coordinates,"SELECT DISTINCT ?item ?name ?coord ?lat ?lon
WHERE
{
 hint:Query hint:optimizer ""None"" .
 ?item wdt:P131* wd:Q1492 .
 ?item wdt:P31/wdt:P279* wd:Q33506 .
 ?item wdt:P625 ?coord .
 ?item p:P625 ?coordinate .
 ?coordinate psv:P625 ?coordinate_node .
 ?coordinate_node wikibase:geoLatitude ?lat .
 ?coordinate_node wikibase:geoLongitude ?lon .
 SERVICE wikibase:label {
 bd:serviceParam wikibase:language ""ca"" .
 ?item rdfs:label ?name
 }
}
ORDER BY ASC (?name)",https://query.wikidata.org/sparql
